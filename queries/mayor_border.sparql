```

PREFIX schema: <http://schema.org/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX ldgeo:  <https://ld.geo.admin.ch/def/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>

SELECT * WHERE {
	?muni schema:name ?muniName;
	      rdfs:seeAlso ?wikiURI;
	      dct:hasVersion ?version.
  
     	?version dct:issued ?versionDate;
		 geo:hasGeometry ?geometry.
	
	?geometry geo:asWKT ?coords.
	
	FILTER (?versionDate = "2017-01-01"^^xsd:date)
	{
		SELECT ?wikiURI (xsd:string(?flagLink) AS ?flag) ?mayorLabel ?polPartyLabel WHERE {
			
			SERVICE <https://query.wikidata.org/bigdata/namespace/wdq/sparql> {
				
				?wikiURI wdt:P31 wd:Q70208;
					     wdt:P6 ?mayor;
					     wdt:P94 ?flagLink.
				
				?mayor rdfs:label ?mayorLabel;
				       wdt:P102 ?polParty.
				
				?polParty rdfs:label ?polPartyLabel.
				
				FILTER (lang(?mayorLabel) = "en")
				FILTER (lang(?polPartyLabel) = "en")
			}
		}
	}
	BIND (CONCAT(?muniName, "<br>Mayor: ", ?mayorLabel, "<br>Party: ", ?polPartyLabel, "<br><a href='", ?flag, "' target='_blank'>Flag</a>") AS ?coordsLabel)
}

```
